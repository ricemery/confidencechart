import java.text.SimpleDateFormat

plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'com.google.osdetector'           version '1.7.0'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'net.nemerosa.versioning'         version '2.15.1'
    id 'biz.aQute.bnd.builder'           version '6.1.0'
}

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'maven-publish'

description   = 'JavaFx Confidence Plot library'

Date buildTimeAndDate = new Date()
ext {
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform        = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-swing:${javafxVersion}:$platform"
    implementation "eu.hansolo:toolbox:17.0.6"
    implementation "eu.hansolo:toolboxfx:17.0.15"
}

jar {
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'com.chainstaysoftware.confidenceplot',
                'Export-Package'        : 'com.chainstaysoftware.confidenceplot.charts, com.chainstaysoftware.confidenceplot.charts.data, com.chainstaysoftware.confidenceplot.charts.event, com.chainstaysoftware.confidenceplot.charts.series, com.chainstaysoftware.confidenceplot.charts.tools'
        )
    }
}

// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
